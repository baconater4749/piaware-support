#!/usr/bin/env tclsh

#
# generate dump1090 and beast-splitter configuration
#

package require fa_config_generator

proc generate_receiver_config {config} {
	generate_beast_splitter_config $config
	generate_dump1090_config $config
	generate_dump978_config $config
	return 1
}

proc generate_beast_splitter_config {config} {
	if {![$config get allow-modeac]} {
		# disable A/C on the input side
		set force "--force j"
	} else {
		set force ""
	}

	switch -nocase [$config get receiver-type] {
		beast {
			lappend relayconfig "ENABLED=yes"

			set baud [$config get beast-baudrate]
			if {$baud ne ""} {
				lappend relayconfig "INPUT_OPTIONS=\"--serial /dev/beast --fixed-baud $baud $force\""
			} else {
				lappend relayconfig "INPUT_OPTIONS=\"--serial /dev/beast $force\""
			}
		}

		relay {
			lappend relayconfig "ENABLED=yes"
			lappend relayconfig "INPUT_OPTIONS=\"--net [$config get receiver-host]:[$config get receiver-port] $force\""
		}

		radarcape {
			lappend relayconfig "ENABLED=yes"
			lappend relayconfig "INPUT_OPTIONS=\"--net [$config get radarcape-host]:10003 $force\""
		}

		default {
			lappend relayconfig "# beast-splitter does not run when receiver-type=[$config get receiver-type]"
			lappend relayconfig "ENABLED=no"
		}
	}

	lappend relayconfig "OUTPUT_OPTIONS=\"--listen 30005:R --connect localhost:30104:R\""
	::fa_config_generator::generate_file "/etc/default/beast-splitter" $relayconfig
	return 1
}

proc generate_dump1090_config {config} {
	set receiverOpts [list]
	set enabled 1

	switch -nocase [$config get receiver-type] {
		rtlsdr {
			set boPort 30005
			lappend receiverOpts "--device-index" [$config get rtlsdr-device-index]
			set gain [$config get rtlsdr-gain]
			if {$gain eq "max"} {
				lappend receiverOpts "--gain" -10
			} else {
				lappend receiverOpts "--gain" $gain
			}
			lappend receiverOpts "--ppm" [$config get rtlsdr-ppm]
		}

		bladerf {
			set boPort 30005
			lappend receiverOpts "--device-type" "bladerf"
			lappend receiverOpts "--gain" "66"
			lappend receiverOpts "--bladerf-fpga" "/usr/share/dump1090-fa/bladerf/decimate8-x40.rbf"
			lappend receiverOpts "--bladerf-decimation" "8"
			lappend receiverOpts "--bladerf-bandwidth" "14000000"
			lappend receiverOpts "--freq" "1085200000"
		}

		beast - radarcape - relay {
			set boPort 0   ;# 30005 output is provided by beast-splitter
			lappend receiverOpts "--net-only"
		}

		none - other {
			set enabled 0
		}

		default {
			::fa_config_generator::logger "unrecognized receiver-type '[$config get receiver-type]', not configuring dump1090"
			set enabled 0
		}
	}

	if {![$config get allow-modeac]} {
		lappend receiverOpts "--no-modeac-auto"
	}

	set dump1090config [list]
	if {$enabled} {
		lappend dump1090config "ENABLED=yes"
		lappend dump1090config {DECODER_OPTIONS="--max-range 360 --fix"}
		lappend dump1090config "NET_OPTIONS=\"--net --net-heartbeat 60 --net-ro-size 1300 --net-ro-interval 0.2 --net-http-port 0 --net-ri-port 0 --net-ro-port 30002 --net-sbs-port 30003 --net-bo-port $boPort --net-bi-port 30004,30104\""
		lappend dump1090config {JSON_OPTIONS="--json-location-accuracy 2"}
		lappend dump1090config "RECEIVER_OPTIONS=\"[join $receiverOpts " "]\""
	} else {
		lappend dump1090config "# dump1090-fa does not run when receiver-type=[$config get receiver-type]"
		lappend dump1090config "ENABLED=no"
	}

	::fa_config_generator::generate_file "/etc/default/dump1090-fa" $dump1090config
	return 1
}

proc generate_dump978_config {config} {
	set receiverOpts [list "--raw-port" "30978" "--json-port" "30979"]
	set enabled 1

	switch -nocase [$config get uat-receiver-type] {
		sdr {
			set gain [$config get uat-sdr-gain]
			if {$gain ne "max"} {
				# default is max gain, only need to override if different
				lappend receiverOpts "--sdr-gain" $gain
			}
			set ppm [$config get uat-sdr-ppm]
			if {$ppm ne 0} {
				lappend receiverOpts "--sdr-ppm" $ppm
			}
			lappend receiverOpts "--sdr" [$config get uat-sdr-device]
		}

		stratuxv3 {
			lappend receiverOpts "--stratuxv3" "/dev/uatradio"
		}

		none - other {
			set enabled 0
		}

		default {
			::fa_config_generator::logger "unrecognized uat-receiver-type '[$config get uat-receiver-type]', not configuring dump978"
			set enabled 0
		}
	}

	set dump978config [list]
	set skyaware978config [list]
	if {$enabled} {
		lappend dump978config "ENABLED=yes"
		lappend dump978config "RECEIVER_OPTIONS=\"[join $receiverOpts " "]\""

		lappend skyaware978config "ENABLED=yes"
		lappend skyaware978config {NET_OPTIONS="--connect localhost:30978 --reconnect-interval 30"}
		lappend skyaware978config {HISTORY_OPTIONS="--history-count 120 --history-interval 30"}
	} else {
		lappend dump978config "# dump978-fa does not run when uat-receiver-type=[$config get uat-receiver-type]"
		lappend dump978config "ENABLED=no"

		lappend skyaware978config "# skyaware978 does not run when uat-receiver-type=[$config get uat-receiver-type]"
		lappend skyaware978config "ENABLED=no"
	}

	::fa_config_generator::generate_file "/etc/default/dump978-fa" $dump978config
	::fa_config_generator::generate_file "/etc/default/skyaware978" $skyaware978config

	return 1
}

if {!$tcl_interactive} {
	::fa_config_generator::generate_from_argv generate_receiver_config $argv
}
